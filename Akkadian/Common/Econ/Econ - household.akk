# Namespace:    Hammurabi.Econ
# Summary:      Basic economic relationships related to households
# Updated:      2012-08-20
# Author:       Michael Poulshock


# HOUSEHOLDS

# Household size
Tnum HouseholdSize(Thing p) =
    HouseholdMembersOf(p).Count

# TODO: This currently does not include the person!
# >>Who are/were the other members of {1}'s household?
Tset HouseholdMembersOf(Thing person)

# Person's household as a unit
# TODO: This doesn't get added to unit tests generated by Hammurabi Interactive
Thing HouseholdOf(Thing person) =
    AddThing(person.Id + "'s household")

    
# RELATIONSHIPS BETWEEN TWO PEOPLE
    
# Two people are members of the same household.
Tbool ShareHousehold(Thing p1, Thing p2) =
    LivesWith(p1, p2)     # simplifying assumption
                 
# >>Do/did {1} and {2} share a principal abode?
TboolSym SharePrincipalAbode(Thing p1, Thing p2) =
    LivesWith(p1, p2)     # simplifying assumption

# >>Do/did {1} and {2} live together?
TboolSym LivesWith(Thing p1, Thing p2)

# >>Does/did {1} provide financial support for {2}?
Tbool ProvidesSupportFor(Thing p1, Thing p2)

# >>Do/did {1} and {2} purchase and prepare meals together (for home consumption)?
Tbool PurchaseAndPrepareMealsTogether(Thing p1, Thing p2) 

PurchaseAndPrepareMealsTogether(1,2) assumes LivesWith(1,2)


# UNIT TESTS

Test: 714127497
- Things t1, t2
- Things moe, harry, larry
- Econ.HouseholdMembersOf(t1) = [[moe, harry, larry]]
- Econ.HouseholdSize(t1).TestOutput =?= "Time.DawnOf 3 "

Test: TboolSym1
- Thing a
- Thing b
- Econ.SharePrincipalAbode(a,b) = true
- Econ.SharePrincipalAbode(a,b).Out =?= true

Test: TboolSym2
- Thing a
- Thing b
- Econ.SharePrincipalAbode(a,b) = true
- Econ.SharePrincipalAbode(b,a).Out =?= true

Test: ShareAbode
- Thing a
- Thing b
- Econ.LivesWith(a,b) = true
- Econ.SharePrincipalAbode(b,a).Out =?= true

Test: LivesWith1
- Thing a
- Thing b
- Econ.LivesWith(a,b) = true
- Econ.LivesWith(a,b).Out =?= true

Test: LivesWith2
- Thing a
- Thing b
- Econ.LivesWith(a,b) = true
- Econ.LivesWith(b,a).Out =?= true



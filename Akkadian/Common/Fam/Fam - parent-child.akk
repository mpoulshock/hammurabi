# Namespace:    Hammurabi.Fam
# Summary:      Basic family relationships: parent-child
# Updated:      2012-06-22
# Author:       Michael Poulshock


# PARENT-CHILD RELATIONSHIPS

# Parents married on a particular date
Tbool ParentsMarriedAsOf(Person p, Tdate date) =
	Fam.AreMarried(mom,dad).AsOf(date)
	
	Person mom =
		MotherAsOf(p, date)
	
	Person dad =
		FatherAsOf(p, date)

# Mother on a particular date
# Warning: returns null if more than one mother
Person MotherAsOf(Person p, Tdate date) =
	Fam.ParentsOf(p).AsOf(date).Filter(IsMotherOf(_,p)).ToPerson
	
# Is mother of
Tbool IsMotherOf(Person p1, Person p2) =
	IsParentOf(p1,p2) &
	IsFemale(p1)

# Father on a particular date
# Warning: returns null if more than one father
Person FatherAsOf(Person p, Tdate date) =
	Fam.ParentsOf(p).AsOf(date).Filter(IsFatherOf(_,p)).ToPerson
	
# Is father of
Tbool IsFatherOf(Person p1, Person p2) =
	IsParentOf(p1,p2) &
	IsMale(p1)
	
# Person's parents
Tset ParentsOf(Person p) =
	AllPeopleExcept(p).Filter(IsParentOf(_,p))

# Person's children
Tset ChildrenOf(Person p) =
	AllPeople.Filter(IsParentOf(p,_))

# Child
# Not strictly necessary but aids readability
Tbool IsChildOf(Person p1, Person p2) =
	IsParentOf(p2,p1)
	
# One person is the parent of another
TboolIn IsParentOf(Person p1, Person p2) =
	IsBiologicalParentOf(p1, p2) |
	IsAdoptiveParentOf(p1, p2) |
	IsFosterParentOf(p1, p2) |
	IsStepparentOf(p1, p2)
            
# Biological parent
Tbool IsBiologicalParentOf(Person p1, Person p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Biological parent", "Biological child")

# Adoptive parent
Tbool IsAdoptiveParentOf(Person p1, Person p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Adoptive parent", "Adopted child")

# Foster parent
Tbool IsFosterParentOf(Person p1, Person p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Foster parent", "Foster child")

# Stepparent
Tbool IsStepparentOf(Person p1, Person p2) =
	Facts.Sym(p1, "FamilyRelationship", p2, "Stepparent", "Stepchild")

# Pregnant
TboolIn IsPregnant(Person p)

# Unborn - in order to reason about fetuses
TboolIn IsUnborn(Person p)


# PATERNITY, LEGITIMATION, ETC.

# Establishes paternity
Tbool PaternityEstablishedFor(Person parent, Person child) =
	IsMale(parent) &
	is12YearAgeDiff &
	TboolIn PaternityEstablishedFor(parent,child) &
	TheTime.IsAtOrAfter(date)
	
	Tbool is12YearAgeDiff =
		Age(parent) - Age(child) >= 12
	
	Tdate date =
		TdateIn DatePaternityEstablishedFor(parent,child)
		
# Illegitimate child
Tbool IsIllegitimate(Person p) =
	!ParentsMarriedAsOf(p, DoB(p))

# Legitimated by father (he acknowledged parental responsibility)
# Example of temporal value built from threshold facts and an event
Tbool Legitimated(Person parent, Person child) =
	IsFatherOf(parent, child) &
	IsIllegitimate(child) &
	TboolIn Legitimated(parent,child) &
	TheTime.IsAtOrAfter(date)
	
	Tdate date =
		TdateIn DateLegitimated(parent,child)

# Legally committed to financially support child
Tbool CommittedToFinanciallySupport(Person p1, Person p2) =
	TboolIn CommittedToFinanciallySupport(p1,p2) &
	TheTime.IsAtOrAfter(date)
	
	Tdate date =
		TdateIn DateCommittedToFinanciallySupport(p1,p2)
		
		
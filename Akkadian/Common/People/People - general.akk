# Namespace:    Hammurabi.H
# Summary:      Basic facts about individual people.
# Updated:      2012-09-08
# Author:       Michael Poulshock

    
# BIRTH, LIFE, AND DEATH

# >>What is {1}'s date of birth?
Tdate DoB(Thing p)

# >>What is {1}'s date of death?
Tdate DateOfDeath(Thing p)

# >>Is {1} deceased?
Tbool IsDeceased(Thing p)

# Age (in years)
Tnum Age(Thing p) =
    Time.IntervalsSince(DoB(p),
                        DoB(p).AddYears(110),
                        Time.IntervalType.Year)
                        
# Alive
Tbool IsAlive(Thing p) =
    ...
        !IsDeceased(p) &
        TheTime.IsBetween(DoB(p), DoB(p).AddYears(120))        # TODO: Reconsider...
    |
    ...
        IsDeceased(p) &
        TheTime.IsBetween(DoB(p), DateOfDeath(p))
        
# Age in months (temporal)
Tnum AgeInMonths(Thing p) =
    Time.IntervalsSince(DoB(p), DoB(p).AddYears(110), Time.IntervalType.Month)

        
# GENDER

# Female
Tbool IsFemale(Thing p) =
    Gender(p) == "Female"

# Male
Tbool IsMale(Thing p) =
    Gender(p) == "Male"

# Transgendered
Tbool IsTransgendered(Thing p) =
    Gender(p) == "Transgendered"        

# >>What is {1}'s gender? (Options: Male, Female, Transgendered)
Tstr Gender(Thing p)


# ETHNICITY

# Member of a Native American tribe
# Includes Alaska Natives and Native Hawaiians
# Note: Most legal sources use "Indian" or "American Indian"
# >> Is {1} a member of a Native American, Native Hawaiian, or Alaska Native tribe?
Tbool IsNativeAmerican(Thing p)


# MISCELLANEOUS

# >>Does/did {1} have a valid Social Security Number?
Tbool HasSSN(Thing p) 
    
# >>Has {1} applied for (but not yet received) a Social Security Number?
Tbool AppliedForSSN(Thing p) 

# See 42 USC 416(i)(1)(B).
# >>Is/was {1} legally blind?
Tbool IsBlind(Thing p)

# >>Does/did {1} have a physical or mental impairment that substantially limits one or more major life activities (see 42 USC 12102)?
Tbool IsDisabled(Thing p)

# >>Is/was {1} incapable of self-care?
Tbool IsIncapableOfSelfCare(Thing p) 

# >>Is/was {1} incarcerated or in prison?
Tbool IsInPrison(Thing p)

# >>Is {1} a fleeing felon?
Tbool IsFleeingFelon(Thing p)

# >>Is/was {1} in a juvenile rehabilitation facility?
Tbool InJuvenileFacility(Thing p)

# >>Is/was {1} in foster care?
Tbool InFosterCare(Thing p)

# >>Is/was {1} in a drug or alcohol treatment program?
Tbool InDrugOrAlcoholTreatmentProg(Thing p) 
    
    
# UNIT TESTS

Test: 193653325
- Things t1, t2
- IsDeceased(t1) = true
- DoB(t1) = 2000-01-02
- DateOfDeath(t1) = 2088-03-02
- Hammurabi.H.IsAlive(t1).Out =?= "{Dawn: false; 1/2/2000: true; 3/2/2088: false}"

Test: 143320034
- Things t1, t2
- IsDeceased(t1) = false
- DoB(t1) = 2011-01-01
- Hammurabi.H.IsAlive(t1).Out =?= "{Dawn: false; 1/1/2011: true; 1/1/2131: false}"

Test: 773795755
- Things t1, t2
- Gender(t1) = "Female"
- Hammurabi.H.IsFemale(t1).Out =?= true

Test: 308454842
- Things t1, t2
- Gender(t1) = "Male"
- Hammurabi.H.IsFemale(t1).Out =?= false

Test: TotalSanity
- Thing p
- DoB(p) = 1940-01-02
- DoB(p).Out =?= 1940-01-02
                            
Test: Alive1
- Thing p
- IsAlive(p).Out =?= "Unstated"

Test: Alive2a
- Thing p
- DoB(p) = 2000-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2010-01-01).Out =?= true

Test: Alive2b
- Thing p
- DoB(p) = 2000-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2150-01-01).Out =?= false

Test: Alive2c
- Thing p
- DoB(p) = 1999-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2010-01-01).Out =?= true

Test: Alive3a
- Thing p
- DoB(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(2100-01-01).Out =?= false

Test: Alive3b
- Thing p
- DoB(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(2050-01-01).Out =?= true

Test: Alive3c
- Thing p
- DoB(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(1999-01-01).Out =?= false



# Namespace:    Hammurabi.H
# Summary:      Basic facts about individual people.
# Updated:      2012-07-16
# Author:       Michael Poulshock


# BIRTH, LIFE, AND DEATH

# Date of birth
TdateIn DoB(Person p) =
	TdateIn DateOfBirth(p)

# Date of death
TdateIn DateOfDeath(Person p)

# Deceased
TboolIn IsDeceased(Person p)

# Age (in years)
Tnum Age(Person p) =
	Time.IntervalsSince(DoB(p),
	                    DoB(p).AddYears(110),
						Time.IntervalType.Year)
						
# Alive
Tbool IsAlive(Person p) =
	...
		!IsDeceased(p) &
		TheTime.IsBetween(DoB(p), DoB(p).AddYears(120))
	|
	...
		IsDeceased(p) &
		TheTime.IsBetween(DoB(p), DateOfDeath(p))
		
# Age in months (temporal)
Tnum AgeInMonths(Person p) =
	Time.IntervalsSince(DoB(p), DoB(p).AddYears(110), Time.IntervalType.Month)

		
# GENDER

# Female
Tbool IsFemale(Person p) =
	Gender(p) == "Female"

# Male
Tbool IsMale(Person p) =
	Gender(p) == "Male"

# Transgendered
Tbool IsTransgendered(Person p) =
	Gender(p) == "Transgendered"		

# Gender
TstrIn Gender(Person p)


# ETHNICITY

# Member of a Native American tribe
# Includes Alaska Natives and Native Hawaiians
# Note: Most legal sources use "Indian" or "American Indian"
TboolIn IsNativeAmerican(Person p)


# MISCELLANEOUS

# Has a Social Security Number
TboolIn HasSSN(Person p) 
	
# Has applied for (but not yet received) an SSN
TboolIn AppliedForSSN(Person p) 

# Blind (see 42 USC 416(i)(1)(B))
TboolIn IsBlind(Person p)

# Disabled 
# Has a physical or mental impairment that substantially 
# limits one or more major life activities (see 42 USC 12102)
TboolIn IsDisabled(Person p)

# Incapable of self-care
TboolIn IsIncapableOfSelfCare(Person p) 

# In prison
TboolIn IsInPrison(Person p)

# In juvenile rehab facility
TboolIn InJuvenileFacility(Person p)

# In a drug or alcohol treatment program
TboolIn InDrugOrAlcoholTreatmentProg(Person p) 
	
	
# UNIT TESTS

Test: TotalSanity
- Person p
- DateOfBirth(p) = 1940-01-02
- DoB(p).Out =?= 1940-01-02
							
Test: Alive1
- Person p
- IsAlive(p).Out =?= "Unstated"

Test: Alive2a
- Person p
- DateOfBirth(p) = 2000-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2010-01-01).Out =?= true

Test: Alive2b
- Person p
- DateOfBirth(p) = 2000-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2150-01-01).Out =?= false

Test: Alive2c
- Person p
- DateOfBirth(p) = 1999-01-01
- IsDeceased(p) = false
- IsAlive(p).AsOf(2010-01-01).Out =?= true

Test: Alive3a
- Person p
- DateOfBirth(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(2100-01-01).Out =?= false

Test: Alive3b
- Person p
- DateOfBirth(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(2050-01-01).Out =?= true

Test: Alive3c
- Person p
- DateOfBirth(p) = 2000-01-01
- DateOfDeath(p) = 2090-01-01
- IsDeceased(p) = true
- IsAlive(p).AsOf(1999-01-01).Out =?= false

	

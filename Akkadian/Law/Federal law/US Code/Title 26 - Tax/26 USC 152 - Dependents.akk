# Citation:     26 U.S.C. 152 (2011)
# Namespace:    USC.Tit26.Sec152
# Summary:      Determines whether one person is the "dependent" of another under IRC, Section 152.
# Remarks:      Subsections c4, d3-5, e, and f have not been modeled.  
# Updated:      2012-06-01
# Author:       Michael Poulshock


# >>Is/was {1} a dependent (for tax purposes) of {2}?
TboolIn? IsDependentOf(Thing d, Thing tp) =
    ...
        IsQualifyingChildOf(d, tp) | 
        IsQualifyingRelativeOf(d, tp)
    &
    !CannotBeADependentOf(d, tp)


# QUALIFYING CHILD
	
# Indicates whether one person is a "qualifying child" of another.
Tbool IsQualifyingChildOf(Thing d, Thing tp) =    
    QCGenTest(d, tp, taxYear) &
    QCRelationTest(d, tp) &
    QCAgeTest(d, tp, taxYear) &
	if TboolIn CanBeClaimedAsQCByTwoTaxpayers(d) then Stub()

    Tnum taxYear = Sec441.TaxYear(tp)

	
# Qualifying child - c1 - general elements (excluding those in c2, c3, and c4)
Tbool QCGenTest(Thing d, Thing tp, Tnum taxYear) =
    Econ.SharePrincipalAbode(d,tp).TotalElapsedDaysPer(taxYear) > 182.5 &
    Econ.PercentSelfSupport(d) < 50 &        # per TY
    ...
        !Sec6013.IsMFJ(d) | 
		Sec6013.FilesMFJOnlyToClaimRefund(d)
    
    
# Qualifying child - c2 - relationship test
Tbool QCRelationTest(Thing d, Thing tp) =
    Fam.IsParentOf(tp,d) |
    Fam.IsGrandparentOf(tp,d) |
    Fam.IsGreatGrandparentOf(tp,d) |
    Fam.AreSiblings(tp,d) |
    Fam.AreStepsiblings(tp,d) 		# or descendant of sib or step-sib
        
		
# Qualifying child - c3 - age test
Tbool QCAgeTest(Thing d, Thing tp, Tnum taxYear) =
    ...
        Age(d) < Age(tp) &
        ...
            (Age(d) < 19).AlwaysPer(taxYear) | 
            ...
                Edu.IsStudent(d) &
                (Age(d) < 24).AlwaysPer(taxYear)
    |
    Sec22.IsDisabledPT(d)


# QUALIFYING RELATIVE
	
# Indicates whether one person is a "qualifying relative" of another.
# Subsections d3 and d4 are omitted.
Tbool IsQualifyingRelativeOf(Thing d, Thing tp) =
    genTest & 
    relationTest & 
    d4Test

    # d1 - general elements (excluding those in d2-5)
    # gen test
    Tbool genTest = 
        ...
            Sec61.GrossIncome(d) == 0 |                         # shortcut
            Sec61.GrossIncome(d) < Sec151.ExemptionAmount(d)	# per TY
        & 
		! IsQualifyingChildOf(d, tp) &
		TboolIn NotQualifyingChildOfAnyTaxpayer(d)
		
    # d2 - relationship test
    Tbool relationTest = 
        Fam.IsParentOf(tp,d) |             # A
        Fam.IsGrandparentOf(tp,d) |
        Fam.IsGreatGrandparentOf(tp,d) |
        Fam.AreSiblings(tp,d) |            # B
        Fam.AreStepsiblings(tp,d) |
        Fam.IsParentOf(d,tp) |             # C
        Fam.IsGrandparentOf(d,tp) |
        Fam.IsGreatGrandparentOf(d,tp) |
        Fam.IsStepparentOf(d,tp) |         # D
        Fam.IsAuntOrUncleOf(tp,d) |        # E
        Fam.IsAuntOrUncleOf(d,tp) |        # F
        ...
            Econ.SharePrincipalAbode(tp,d) &
            Econ.ShareHousehold(tp,d)     # H - and not spouse
            
    # d4 - multiple support agreements 
    Tbool d4Test = 
		if TboolIn AnotherTaxpayerProvidedSupportFor(d) then Stub()


# EXCEPTIONS
		
# Exceptions to whether a person can be a dependent. 152(b).
Tbool CannotBeADependentOf(Thing d, Thing tp) =
    !depTest | 
    !jointReturnTest | 
    !citizenshipTest

    # b1 - taxpayer cannot be a dependent & potential dependent cannot have a dependent
    # Note: searching the fact base for possible dependents and defining this test
    # recursively leads to infinite loops and probably is a case of overengineering.
    Tbool depTest =
		! TboolIn CanClaimSomeoneAsDependent(d) &
		! CanBeClaimedAsDependentBySomeone(tp)
            
    # b2 - potential dependent cannot be married filing jointly
    # See IRS Publication 501 (2010), page 10.
    Tbool jointReturnTest = 
        if Sec6013.IsMFJ(d) then TboolIn MFJOnlyForRefund(d)
            
    # b3A - citizenship status
    Tbool citizenshipTest = 
        IsUSCitizen(d) |
        IsUSResident(d) |
        IsResidentOf(d, "Canada") |
        IsResidentOf(d, "Mexico") | 
        adoptionTest

	# b3B - potential dependent must meet the citizenship test
    Tbool adoptionTest = 
        Fam.IsAdoptiveParentOf(tp,d) &
        Econ.SharePrincipalAbode(d,tp) &
        Econ.ShareHousehold(d,tp) &
        ...
            IsUSCitizen(tp) | 
            Imm.IsUSNational(tp)


# FIGURE OUT WHO CAN CLAIM A PERSON AS A DEPENDENT (RECURSIVE)			

# >>Can someone claim {1} as a dependent?
TboolIn? CanBeClaimedAsDependentBySomeone(Thing p) =
	PeopleWhoMightClaimAsDependent(p).Exists(IsDependentOf(p,_))
	
# >>Who has financial or custodial responsibility for {1}?
TsetIn PeopleWhoMightClaimAsDependent(Thing p)
			
			
# UNIT TESTS

Test: Pub501_Simple1
- Thing c		# the child
- Thing t		# the taxpayer
- IsParentOf(t,c) = true
- LivesWith(c,t) = true
- DoB(c) = 2001-01-01
- IsDeceased(c) = false 	#
- IsUSCitizen(c) = true
- CanBeClaimedAsQCByTwoTaxpayers(c) = false
- PercentSelfSupport(c) = 0
- CanClaimSomeoneAsDependent(c) = false
- CanBeClaimedAsDependentBySomeone(t) = false
- AnotherTaxpayerProvidedSupportFor(c) = false
- GrossIncome(c) = 0
- Sec152.IsDependentOf(c,t).AsOf(2010-12-31).Out =?= "Unstated"

##
Test: Pub501_Simple2
- Thing c	
- Thing t	
- IsParentOf(t,c) = true
- LivesWith(c,t) = true
- DoB(c) = 2001-01-01
- DoB(t) = 1970-01-01
- IsDeceased(c) = false 	#
- IsUSCitizen(c) = true
- CanBeClaimedAsQCByTwoTaxpayers(c) = false
- PercentSelfSupport(c) = 0
- CanClaimSomeoneAsDependent(c) = false
- CanBeClaimedAsDependentBySomeone(t) = false
- AnotherTaxpayerProvidedSupportFor(c) = false
- FedTaxFilingStatus(t) = "Single"
- GrossIncome(c) = 0
- Sec152.IsDependentOf(c,t).AsOf(2010-12-31).Out =?= true

# IRS Pub. 501 (2010), Page 10, Example 1.
# Cannot be a dependent because fails joint return test.
Test: Pub501_1
- Thing t		# Taxpayer
- Thing d 		# T's daughter
- Thing s		# T's son-in-law
- FamilyRelationship(d,s) = "Spouse"
- FilesJointFedTaxReturnWith(d,s) = true
- FilesMFJOnlyToClaimRefund(d) = false
- IsParentOf(t,d) = true
- LivesWith(t,d) = true
- DoB(d) = 1992-01-01
- InArmedForces(s) = true
- IsUSCitizen(d) = true
- Sec152.IsDependentOf(d,t).AsOf(2010-12-31).Out =?= false

# IRS Pub. 501 (2010), Page 10, Example 2 (son).
Test: Pub501_2a
- Thing t		# Taxpayer
- Thing d 		# T's daughter
- Thing s		# T's son-in-law
- FamilyRelationship(d,s) = "Spouse"
- FilesJointFedTaxReturnWith(d,s) = true
- FilesMFJOnlyToClaimRefund(d) = true
- IsParentOf(t,d) = true
- DoB(s) = 1992-01-01
- DoB(d) = 1992-01-01
- IsUSCitizen(s) = true
- CanClaimSomeoneAsDependent(s) = false
- CanBeClaimedAsDependentBySomeone(t) = false
- Sec152.CannotBeADependentOf(d,t).AsOf(2010-12-31).Out =?= false

# IRS Pub. 501 (2010), Page 10, Example 2 (daughter-in-law).
Test: Pub501_2b
- Thing t		# Taxpayer
- Thing d 		# T's daughter-in-law
- Thing s		# T's son
- FamilyRelationship(d,s) = "Spouse"
- FilesJointFedTaxReturnWith(d,s) = true
- FilesMFJOnlyToClaimRefund(d) = true
- IsParentOf(t,d) = true
- DoB(s) = 1992-01-01
- DoB(d) = 1992-01-01
- IsUSCitizen(d) = true
- CanClaimSomeoneAsDependent(s) = false
- CanBeClaimedAsDependentBySomeone(t) = false
- Sec152.CannotBeADependentOf(d,t).AsOf(2010-12-31).Out =?= false

# IRS Pub. 501 (2010), Page 10, Example 3.
Test: Pub501_3
- Thing t		# Taxpayer
- Thing s 		# Son
- Thing d		# Daughter-in-law
- DoB(s) = 1992-01-01
- DoB(d) = 1991-01-01
- FamilyRelationship(d,s) = "Spouse"
- IsUSCitizen(s) = true
- FilesJointFedTaxReturnWith(d,s) = true
- FileMFJOnlyToClaimRefund(s) = false
- CanClaimSomeoneAsDependent(s) = false
- CanBeClaimedAsDependentBySomeone(t) = false
- Sec152.CannotBeADependentOf(s,t).AsOf(2010-12-31).Out =?= true

# IRS Pub. 501 (2010), Page 11, Unnumbered example.
Test: Pub501_4
- Thing t 		# Taxpayer
- Thing s		# Son
- DoB(s) = 1991-12-10
- IsFullTimeStudent(s) = false
- IsUSCitizen(s) = true
- IsPermanentlyAndTotallyDisabled(s) = false
- Sec152.IsDependentOf(s,t).AsOf(2010-12-31).Out =?= false

# IRS Pub. 501 (2010), Page 11, Unnumbered example.
Test: Pub501_5
- Thing t 		# Taxpayer
- Thing s		# Son
- DoB(s) = 1991-12-10
- IsFullTimeStudent(s) = true
- IsUSCitizen(s) = true
- IsPermanentlyAndTotallyDisabled(s) = false
- Sec152.IsDependentOf(s,t).AsOf(2010-12-31).Out =?= true

# IRS Pub. 501 (2010), Page 11, Unnumbered example.
Test: Pub501_6
- Thing t 		# Taxpayer
- Thing s		# Son
- DoB(s) = 1991-12-10
- IsFullTimeStudent(s) = false
- IsUSCitizen(s) = true
- IsPermanentlyAndTotallyDisabled(s) = true
- Sec152.IsDependentOf(s,t).AsOf(2010-12-31).Out =?= true

# IRS Pub. 501 (2010), Page 11, Example 1.
Test: Pub501_7
- Thing t		# Taxpayer
- Thing s		# T's spouse
- Thing b		# T's brother
- DoB(t) = 1989-01-01
- DoB(s) = 1989-01-01
- FamilyRelationship(t,s) = "Spouse"
- FamilyRelationship(b,t) = "Sibling"
- DoB(b) = 1987-01-01
- IsMarried(b) = false
- LivesWith(t,s) = true
- LivesWith(t,b) = true
- IsDisabled(b) = false
- AreFilingMFJ(t,s) = true
- Sec152.IsDependentOf(b,t).AsOf(2010-12-31).Out =?= false

# IRS Pub. 501 (2010), Page 11, Example 2.
Test: Pub501_8
- Thing t		# Taxpayer
- Thing s		# T's spouse
- Thing b		# T's brother
- DoB(t) = 1989-01-01
- DoB(s) = 1985-01-01
- FamilyRelationship(t,s) = "Spouse"
- FamilyRelationship(b,t) = "Sibling"
- DoB(b) = 1987-01-01
- IsFullTimeStudent(b) = true
- IsMarried(b) = false
- LivesWith(t,s) = true
- LivesWith(t,b) = true
- IsDisabled(b) = false
- AreFilingMFJ(t,s) = true
- Sec152.IsDependentOf(b,t).AsOf(2010-12-31).Out =?= true
##

Test: Basic_1
- Thing tp
- Thing d
- LivesWith(tp,d) = true
- (Econ.SharePrincipalAbode(d,tp).TotalElapsedDaysPer(Sec441.TaxYear(tp)) > 182.5).AsOf(2012-01-01).Out =?= true

Test: Basic_2
- Thing tp
- Thing d
- LivesWith(tp,d) = false
- (Econ.SharePrincipalAbode(d,tp).TotalElapsedDaysPer(Sec441.TaxYear(tp)) > 182.5).AsOf(2012-01-01).Out =?= false

Test: QCGenTest_1
- Thing tp
- Thing d
- LivesWith(tp,d) = false
- Sec152.QCGenTest(d,tp,Sec441.TaxYear(tp)).AsOf(DateTime.Now).Out =?= false

Test: QCGenTest_1a
- Thing tp
- Thing d
- LivesWith(tp,d) = false
- Econ.SharePrincipalAbode(d,tp).AsOf(2012-05-29).Out =?= false

Test: ShareHousehold
- Thing a
- Thing b
- LivesWith(a,b) = true
- Econ.SharePrincipalAbode(a,b).Out =?= true

Test: QCGenTest_2
- Thing tp
- Thing d
- LivesWith(tp,d) = true
- PercentSelfSupport(d) = 100
- Sec152.QCGenTest(d,tp,Sec441.TaxYear(tp)).AsOf(DateTime.Now).Out =?= false

Test: QCGenTest_3
- Thing tp
- Thing d
- LivesWith(tp,d) = true
- PercentSelfSupport(d) = 0
- FedTaxFilingStatus(d) = "not Married filing jointly"
- Sec152.QCGenTest(d,tp,Sec441.TaxYear(tp)).AsOf(DateTime.Now).Out =?= true

Test: QCGenTest_4
- Thing tp
- Thing d
- LivesWith(tp,d) = true
- PercentSelfSupport(d) = 0
- FedTaxFilingStatus(d) = "Married filing jointly"
- FilesMFJOnlyToClaimRefund(d) = true
- Sec152.QCGenTest(d,tp,Sec441.TaxYear(tp)).AsOf(DateTime.Now).Out =?= true

Test: QCGenTest_5
- Thing tp
- Thing d
- LivesWith(tp,d) = true
- PercentSelfSupport(d) = 0
- FedTaxFilingStatus(d) = "Married filing jointly" 
- FilesMFJOnlyToClaimRefund(d) = false	
- Sec152.QCGenTest(d,tp,Sec441.TaxYear(tp)).AsOf(DateTime.Now).Out =?= false

Test: QCRelationship_1
- Thing tp
- Thing d
- FamilyRelationship(tp,d) = "Biological parent"
- Sec152.QCRelationTest(d,tp).AsOf(DateTime.Now).Out =?= true

Test: QCAge_1
- Thing tp
- Thing d
- DoB(tp) = 1975-01-01
- DoB(d) = 2005-01-01
- IsStudent(d) = false
- IsPermanentlyAndTotallyDisabled(d) = false
- Sec152.QCAgeTest(d,tp,Sec441.TaxYear(tp)).AsOf(2012-03-03).Out =?= true

Test: QCAge_2
- Thing tp
- Thing d
- DoB(tp) = 1975-01-01
- DoB(d) = 1940-01-01
- IsStudent(d) = false
- IsPermanentlyAndTotallyDisabled(d) = true
- Sec152.QCAgeTest(d,tp,Sec441.TaxYear(tp)).AsOf(2012-03-03).Out =?= true



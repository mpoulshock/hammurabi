# Citation:     42 U.S.C. 9902 (2013)
# Namespace:    USC.Tit42.Sec9902
# Summary:      Federal HHS Poverty Guidelines ("FPL")
# Source:       http://aspe.hhs.gov/poverty/
# Updated:      2013-02-27
# Author:       Michael Poulshock


# nnn% of the FPG
Tnum FPGPercentage(Tnum familySize, Tstr jur, Tnum percentage) =
    (FedPovertyGuideline(familySize,jur) * percentage).RoundDown(1)

# 125% of the FPG
Tnum FPG125(Tnum familySize, Tstr jur) =
    (FedPovertyGuideline(familySize,jur) * 1.25).RoundDown(1)

# 133% of the FPG
Tnum FPG133(Tnum familySize, Tstr jur) =
    (FedPovertyGuideline(familySize,jur) * 1.3333).RoundDown(1)
  
# Federal HHS Poverty Guidelines
Tnum FedPovertyGuideline(Tnum familySize, Tstr jur) =
    set:
    if Geo.Among48ContiguousStates(jur) -> FPL48(familySize)
    if jur == "District of Columbia" -> FPL48(familySize)
    if jur == "Alaska" -> FPLAK(familySize)
    if jur == "Hawaii" -> FPLHI(familySize)
    else Stub()
  
# FPL for the 48 states plus DC
Tnum FPL48(Tnum familySize) =
    BaseLevel48() + (AdditionalAmt48() * (familySize - 1))

Tnum BaseLevel48() =
    set:
    from 2014-01-20 -> Stub()
    from 2013-01-25 -> $11,490
    from 2012-01-26 -> $11,170
    from 2011-01-20 -> $10,890
    else Stub()

Tnum AdditionalAmt48() = 
    set:
    from 2014-01-20 -> Stub()
    from 2013-01-25 -> $4,020
    from 2012-01-26 -> $3,960
    from 2011-01-20 -> $3,820
    else Stub()

# FPL for Alaska
Tnum FPLAK(Tnum familySize) =
    BaseLevelAK() + (AdditionalAmtAK() * (familySize - 1))

Tnum BaseLevelAK() =
    set:
    from 2014-01-20 -> Stub()
    from 2013-01-25 -> $14,350
    from 2012-01-26 -> $13,970
    from 2011-01-20 -> $13,600
    else Stub()

Tnum AdditionalAmtAK() = 
    set:
    from 2014-01-20 -> Stub()
    from 2013-01-25 -> $5,030
    from 2012-01-26 -> $4,950
    from 2011-01-20 -> $4,780
    else Stub()

# FPL for Hawaii
Tnum FPLHI(Tnum familySize) =
    BaseLevelHI() + (AdditionalAmtHI() * (familySize - 1))

Tnum BaseLevelHI() =
    set:
    from 2014-01-20 -> Stub()
    from 2013-01-25 -> $13,230
    from 2012-01-26 -> $12,860
    from 2011-01-20 -> $12,540
    else Stub()

Tnum AdditionalAmtHI() = 
    set:
    from 2014-01-20 -> Stub()
    from 2013-01-25 -> $4,620
    from 2012-01-26 -> $4,550
    from 2011-01-20 -> $4,390
    else Stub()


# UNIT TESTS

Test: FPL1 
- USC.Tit42.Sec9902.FedPovertyGuideline(4, "Texas").AsOf(2012-05-05).Out =?= $23,050

Test: FPL2 
- USC.Tit42.Sec9902.FedPovertyGuideline(7, "New Jersey").AsOf(2013-05-05).Out =?= $35,610


# Citation:     42 U.S.C. 9902 (2011)
# Namespace:    USC.Tit42.Sec9902
# Summary:      Federal HHS Poverty Guidelines ("FPL")
# Source:       http://aspe.hhs.gov/poverty/12poverty.shtml
# Updated:      2012-04-26
# Author:       Michael Poulshock


# 125% of the FPG
Tnum FPG125(Tnum familySize, Tstr jur) =
    (FedPovertyGuideline(familySize,jur) * 1.25).RoundDown(1)

# 133% of the FPG
Tnum FPG133(Tnum familySize, Tstr jur) =
    (FedPovertyGuideline(familySize,jur) * 1.33).RoundDown(1)
    
# Federal HHS Poverty Guidelines
Tnum FedPovertyGuideline(Tnum familySize, Tstr jur) =
    temporal:
    from Time.DawnOf -> Stub()
    from 2011-01-20 -> RateFor2011
    from 2012-01-26 -> RateFor2012
    from 2013-01-15 -> Stub()
    endtemporal
    
    Tnum RateFor2012 =
        set:
        if Geo.Among48ContiguousStates(jur) -> ContigRate2012(familySize)
        if jur == "District of Columbia" -> ContigRate2012(familySize)
        if jur == "Alaska" -> AKRate2012(familySize)
        if jur == "Hawaii" -> HIRate2012(familySize)
        else null
        
    Tnum RateFor2011 =
        set:
        if Geo.Among48ContiguousStates(jur) -> ContigRate2011(familySize)
        if jur == "District of Columbia" -> ContigRate2011(familySize)
        if jur == "Alaska" -> AKRate2011(familySize)
        if jur == "Hawaii" -> HIRate2011(familySize)
        else null

        
# 2012
# Poverty guideline for 48 states + DC
Tnum ContigRate2012(Tnum familySize) =
    match familySize
    1 -> 11170
    2 -> 15130
    3 -> 19090
    4 -> 23050
    5 -> 27010
    6 -> 30970
    7 -> 34930
    8 -> 38890
    else 38890 + (3960 * (familySize-8))

# Poverty guideline for AK
Tnum AKRate2012(Tnum familySize) =
    match familySize
    1 -> 13970
    2 -> 18920
    3 -> 23870
    4 -> 28820
    5 -> 33770
    6 -> 38720
    7 -> 43670
    8 -> 48620
    else 48620 + (4950 * (familySize-8))

# Poverty guideline for HI
Tnum HIRate2012(Tnum familySize) =
    match familySize
    1 -> 12860
    2 -> 17410
    3 -> 21960
    4 -> 26510
    5 -> 31060
    6 -> 35610
    7 -> 40160
    8 -> 44710
    else 44710 + (4550 * (familySize-8))

    
# 2011
# Poverty guideline for 48 states + DC
Tnum ContigRate2011(Tnum familySize) =
    match familySize
    1 -> 10890
    2 -> 14710
    3 -> 18530
    4 -> 22350
    5 -> 26170
    6 -> 29990
    7 -> 33810
    8 -> 37630
    else 37630 + (3820 * (familySize-8))

# Poverty guideline for AK
Tnum AKRate2011(Tnum familySize) =
    match familySize
    1 -> 13600
    2 -> 18380
    3 -> 23160
    4 -> 27940
    5 -> 32720
    6 -> 37500
    7 -> 42280
    8 -> 47060
    else 47060 + (4780 * (familySize-8))

# Poverty guideline for HI
Tnum HIRate2011(Tnum familySize) =
    match familySize
    1 -> 12540
    2 -> 16930
    3 -> 21320
    4 -> 25710
    5 -> 30100
    6 -> 34490
    7 -> 38880
    8 -> 43270
    else 43270 + (4390 * (familySize-8))


    


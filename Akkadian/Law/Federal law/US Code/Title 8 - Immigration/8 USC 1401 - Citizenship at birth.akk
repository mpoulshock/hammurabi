# Citation:     8 U.S.C. 1401 (2012)
# Namespace:    USC.Tit8.Sec1401
# Summary:      When is a child a citizen at birth?
# Supp source:  http://www.ilrc.org/files/nats_chart_a_10.pdf
# Updated:      2012-07-12
# Author:       Michael Poulshock

##
	Assumptions:
	
	* The Defense of Marriage Act, 1 U.S.C. 7, applies (so these rules
	  do not apply to same-sex parents).
	* These rules do not apply to adopted children. Marquez-Marquez v. Gonzales, 
	  455 F.3d 548 (5th Cir.)
##


# U.S. national at birth
Tbool IsUSNational(Thing c) =
	AcquiredCitizenshipAtBirth(c)

# Main rule
Tbool AcquiredCitizenshipAtBirth(Thing c) =
	SubsecA(c) |
	# Subsection (b) omitted b/c it's effectively subsumed by (a)
	SubsecC(c) |
	SubsecD(c) |
	SubsecE(c) |
	SubsecF(c) |
	subsecGApplies |
	SubsecH(c)

	Tbool subsecGApplies =
		set:
		if DoB(c) < 1952-12-24 -> Stub()
		else SubsecG(c)

# (a) - Born in U.S.
Tbool SubsecA(Thing c) =
	BornInUSorOP(c)
	
# (c) - Born outside U.S. and its outlying possessions, 
# both parents citizens, one U.S. resident prior to child's
# birth.
Tbool SubsecC(Thing c) =
	! BornInUSorOP(c) &
	...
		ParentsMarried(c) |
		Sec1409.SubsecAApplies(c)	# Can never happen?
	&
	BothParentsCitizens(c) &
	ParentResidedInUSorOPBeforeBirthOf(c)
	
# (d) - Born outside U.S. and its outlying possessions; one parent 
# citizen, one parent national; citizen parent physically present 
# for a continuous period of one year prior to child's birth
Tbool SubsecD(Thing c) =
	! BornInUSorOP(c) &
	...
		ParentsMarried(c) |
		Sec1409.SubsecAApplies(c)
	&
	OneCitizenOneNationalParent(c) &
	BirthParents(c).Exists(IsCitizenPresentInUSorOP1YearBefore(_, DoB(c)))
	
# (e) - Born in outlying possession; one parent citizen, one parent 
# alien; citizen parent physically present for a continuous period 
# of one year prior to child's birth
Tbool SubsecE(Thing c) =
	Sec1101.IsUSOutlyingPossession(BirthCountry(c)) &
	...
		ParentsMarried(c) |
		Sec1409.SubsecAApplies(c)
	&
	OneCitizenOneAlienParent(c) &
	BirthParents(c).Exists(IsCitizenPresentInUSorOP1YearBefore(_, DoB(c)))
	
# (f) - Person of unknown parentage found in U.S. while under age 5;
# not proved, before age 21, to have been born outside U.S. 
Tbool SubsecF(Thing c) =
	Fam.IsOfUnknownParentage(c) &
	Stub()

# (g) - Born outside U.S.; one citizen parent, one alien parent; 
# citizen parent physically present for certain threshold. 
Tbool SubsecG(Thing c) =
	! BornInUSorOP(c) &
	...
		Sec1401a.Sec1401gAppliesTo(c) |		# Is this what 1401a actually intends?
		...
			ParentsMarried(c) |
			Sec1409.SubsecAApplies(c) | 	# Can never happen?
			Sec1409.SubsecBApplies(c)
		&
		OneCitizenOneAlienParent(c) &
		BirthParents(c).Exists(SubsecGParentTest(_, DoB(c)))

# Citizen parent, prior to child's birth, physically present for a
# total of 5 years, 2 of which were after 14 years of age.  
# Certain governmental service counts towards physical presence.
Tbool SubsecGParentTest(Thing parent, Tdate childDoB) =
	IsUSCitizen(parent) &
	presentYears.IsEverTrueBefore(childDoB) &
	presentYearsBeforeAge14.IsEverTrue()
	
	# How many years they must be present...
	Tbool presentYears =
		present.RunningElapsedYears >= yearThreshold
	
	# See statutory notes; ILRC chart
	Tnum yearThreshold =
		set:
		if childDoB < 1986-11-14 -> 10
		else 5
	
	# How many years they must be present before age 14...
	Tbool presentYearsBeforeAge14 =
		present.RunningElapsedYears >= yearThreshold14 &
		Age(parent) >= 14
	
	# See statutory notes; ILRC chart
	Tnum yearThreshold14 =
		set:
		if childDoB < 1986-11-14 -> 5
		else 2
		
	# Ways to be considered "physically present"
	Tbool present =
		PresentUnderSubsecG(parent)

# "Physically present" for purposes of subsection (g)
Tbool PresentUnderSubsecG(Thing p) =
	PresentInUSorOP(p) |
	Mil.InArmedForces(p) |
	Econ.IsUSGovtEmployee(p) |
	# USC.Tit22.Sec288.IsEmployeeOfIntlOrg(p)
	Stub() 		# Parent is dependent of certain gov't officials
		
# (h) - Rule for children born before 1934ish
# Are the gender assumptions here correct?
Tbool SubsecH(Thing c) =
	ParentsMarried(c) &
	Imm.IsAlien(FatherOf(c)) &
	IsUSCitizen(MotherOf(c)) &
	ResidedInUSorOP(MotherOf(c)).IsEverTrueBefore(DoB(c))


# PLACE OF BIRTH

# Born in U.S. or outlying possession
Tbool BornInUSorOP(Thing c) =
	BornInTheUSA(c) |
	Sec1101.IsUSOutlyingPossession(BirthCountry(c))
	
	
# RESIDENCE IN U.S.

# A parent resided in U.S. at or before child's birth
Tbool ParentResidedInUSorOPBeforeBirthOf(Thing c) =
	ResidedInUSorOP(FatherOf(c)).IsEverTrueBefore(DoB(c)) |
	ResidedInUSorOP(MotherOf(c)).IsEverTrueBefore(DoB(c))

# Resided in U.S. or outlying possession
Tbool ResidedInUSorOP(Thing p) =
	IsUSResident(p) |
	Sec1101.IsUSOutlyingPossession(loc)
	
	Tstr loc = 
		Imm.CountryOfResidence(p)
		

# CONTINUOUS PHYSICAL PRESENCE IN U.S.

# U.S. citizen physically present in U.S. or outlying possession
# for continuous period of one year before a given date
Tbool IsCitizenPresentInUSorOP1YearBefore(Thing p, Tdate date) =
	IsCitizenPresentInUSorOPNYearsBefore(p, 1, date)
	
# U.S. citizen physically present in U.S. or outlying possession
# for continuous period of N years before a given date
# (This rule allows logic reuse.)
Tbool IsCitizenPresentInUSorOPNYearsBefore(Thing p, Tnum years, Tdate date) =
	IsUSCitizen(p) &
	present.IsEverTrueBefore(date)
	
	Tbool present =
		PresentInUSorOP(p).YearsContinuouslyTrue >= years

# >>Was {1} physically present in the U.S. or one of its outlying possessions?
TboolIn PresentInUSorOP(Thing p) =
	PresentInUS(p) |
	Sec1101.IsUSOutlyingPossession(loc)
	
	Tstr loc = 
		CountryLocation(p)

		
# PARENTS' STATUSES	AT TIME OF CHILD'S BIRTH
		
# Both parents are U.S. citizens
Tbool BothParentsCitizens(Thing c) =
	IsUSCitizen(FatherOf(c)).AsOf(DoB(c)) &
	IsUSCitizen(MotherOf(c)).AsOf(DoB(c))

# Both parents are U.S. nationals
Tbool BothParentsNationals(Thing c) =
	Imm.IsUSNational(FatherOf(c)).AsOf(DoB(c)) &
	Imm.IsUSNational(MotherOf(c)).AsOf(DoB(c))
	
# One parent is a U.S. citizen, one is a U.S. national
Tbool OneCitizenOneNationalParent(Thing c) =
	BirthParents(c).Exists(IsUSCitizen(_)).AsOf(DoB(c)) &
	BirthParents(c).Exists(Imm.IsUSNational(_)).AsOf(DoB(c))

# One parent is a U.S. citizen, one is an alien
Tbool OneCitizenOneAlienParent(Thing c) =
	BirthParents(c).Exists(IsUSCitizen(_)).AsOf(DoB(c)) &
	BirthParents(c).Exists(Imm.IsAlien(_)).AsOf(DoB(c))

# One parent is a U.S. national, one is an alien
Tbool OneNationalOneAlienParent(Thing c) =
	BirthParents(c).Exists(Imm.IsUSNational(_)).AsOf(DoB(c)) &
	BirthParents(c).Exists(Imm.IsAlien(_)).AsOf(DoB(c))
	
	
# PARENTS

# Parents married as of child's date of birth
# TODO: Connect to 8 U.S.C. 1409
Tbool ParentsMarried(Thing c) =
	Fam.AreMarried(FatherOf(c), MotherOf(c)).AsOf(DoB(c))
	# ! Fam.IsAdopted(c)
	
# The person's parents at time of birth
Tset BirthParents(Thing c) =
	new Tset(FatherOf(c), MotherOf(c))

# >>Who was {1}'s father at the time of his or her birth?
Thing FatherOf(Thing c) =
	TsetIn FatherAtTimeOfBirth(c).ToThing
	
# >>Who was {1}'s mother at the time of his or her birth?
Thing MotherOf(Thing c) =
	TsetIn MotherAtTimeOfBirth(c).ToThing
	

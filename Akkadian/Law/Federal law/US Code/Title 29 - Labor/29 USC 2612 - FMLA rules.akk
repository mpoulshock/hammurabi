# Citation:     29 U.S.C. 2612 (2011)
# Namespace:    USC.Tit29.Sec2612
# Summary:      Makes determinations related to the Family and Medical Leave Act (FMLA).
# Updated:      2012-06-01
# Author:       Michael Poulshock


# Indicates whether it is during the period in which an employee (e)
# is entitled to FMLA leave from corporation (c).
Tbool IsDuringEligiblePeriod(Person e, Corp c) =
	Stub()

# 2612(a) - Returns the number of weeks of FMLA leave to which an
# employee (e) at corporation (c) is entitled in a 12-month period.
Tnum NumberWeeksEntitled(Person e, Corp c) =
	set:
	if IsEntitledToServiceLeaveFrom(e,c) -> 26
    if IsEntitledToRegLeaveFrom(e,c) -> 12
	else 0
	
# 2612 - Indicates whether an employee (e) is eligible for 
# any FMLA leave from a corporation (c).
Tbool IsEntitledToLeaveFrom(Person e, Corp c) =
	IsEntitledToRegLeaveFrom(e,c) |
	IsEntitledToServiceLeaveFrom(e,c)
				   
# 2612(a)(1) - Indicates whether an employee (e) is eligible for 
# "regular" (non-servicemember) FMLA leave from a corporation (c).
Tbool IsEntitledToRegLeaveFrom(Person e, Corp c) =
	Sec2611.IsEligibleEmployee(e,c) &
	Sec2611.IsCoveredEmployer(c) &
	...
		a1A(e,c) |
		a1B(e,c) |
		a1C(e,c) |
		a1D(e,c) |
		a1E(e,c)
		
# 2612(a)(1)(A) - Birth of child
Tbool a1A(Person e, Corp c) =
	ReasonForLeave(e,c) == "Maternity or paternity"
	
# 2612(a)(1)(B) - Adopted / foster child
Tbool a1B(Person e, Corp c) =
	reason == "To adopt a child" |
	reason == "To become a foster parent"

	Tstr reason = 
		ReasonForLeave(e,c)
	
# 2612(a)(1)(C) - Family member sick
Tbool a1C(Person e, Corp c) =
	ReasonForLeave(e,c) == "To care for family member with a health condition" &
	...
		Fam.AreMarried(e, SickFam(e)) |
		Sec2611.IsChildOf(SickFam(e), e) |
		Sec2611.IsParentOf(SickFam(e), e)
	&
	Sec2611.HasSeriousHealthCondition(SickFam(e))
    
# 2612(a)(1)(D) - Employee sick
Tbool a1D(Person e, Corp c) =
	ReasonForLeave(e,c) == "Employee cannot work due to health condition" &
	Sec2611.HasSeriousHealthCondition(e)
	      
# 2612(a)(1)(E) - Qualifying exigency / active duty
Tbool a1E(Person e, Corp c) =
	ReasonForLeave(e,c) == "Other need arising due to family member serving in Armed Forces"

# 2612(a)(3) - Indicates whether an employee (e) is eligible for
# servicemember family leave from a corporation (c).
Tbool IsEntitledToServiceLeaveFrom(Person e, Corp c) =
	ReasonForLeave(e,c) == "To care for a family member in the Armed Forces" &
	Sec2611.IsCoveredEmployer(c) &
	Sec2611.IsEligibleEmployee(e,c) &
	...
		Fam.AreMarried(e,SickFam(e)) |
		Sec2611.IsChildOf(e,SickFam(e)) | 
		Sec2611.IsParentOf(e,SickFam(e)) |
		Fam.IsNextOfKinOf(e,SickFam(e))
		        
# Returns a person's reason for requesting family leave from an employer.
Tstr ReasonForLeave(Person e, Corp c) =
	TstrIn ReasonForRequestingLeaveFrom(e,c)
	
# Returns the person who the employee needs to take care of.
Person SickFam(Person e) =
	TsetIn NeedsLeaveToProvideCareFor(e).ToPerson

	
# UNIT TESTS

Test: Test1
- Person e
- Corp c
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= "Unstated"

Test: Test2
- Person e
- Corp c
- ReasonForRequestingLeaveFrom(e,c) = "Other"
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= false

Test: Test3
- Person e
- Corp c
- EmploymentRelationship(e,c) = "None"
- Sec2612.IsEntitledToRegLeaveFrom(e,c).Out =?= false

Test: Test4
- Person e
- Corp c
- NumberOfEmployees(c) = 2000
- EmploymentRelationship(e,c) = "Employee"
- HoursWorkedPerWeek(e,c) = 40
- IsEmployeeUnder5USC6301(e) = false
- LessThan50EmployeesWithin75MilesOfWorksite(e,c) = false
- IsAirlineFlightCrew(e,c) = false
- ReasonForRequestingLeaveFrom(e,c) = "Other"
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= false

Test: Test5
- Person e
- Corp c
- NumberOfEmployees(c) = 2000
- EmploymentRelationship(e,c) = "Employee"
- HoursWorkedPerWeek(e,c) = 40
- IsEmployeeUnder5USC6301(e) = false
- LessThan50EmployeesWithin75MilesOfWorksite(e,c) = false
- IsAirlineFlightCrew(e,c) = false
- ReasonForRequestingLeaveFrom(e,c) = "To adopt a child"
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= "Unstated"

Test: Test6
- Person e
- Corp c
- NumberOfEmployees(c) = 2000
- EmploymentRelationship(e,c) = "Employee"
- HoursWorkedPerWeek(e,c) = 40
- IsEmployeeUnder5USC6301(e) = false
- LessThan50EmployeesWithin75MilesOfWorksite(e,c) = false
- IsAirlineFlightCrew(e,c) = false
- ReasonForRequestingLeaveFrom(e,c) = "To adopt a child"
- DateFamilyLeaveBegins(e,c) = 2014-01-01
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= true

Test: Test7_FlightCrew
- Person e
- Corp c
- NumberOfEmployees(c) = 2000
- EmploymentRelationship(e,c) = "Employee"
- HoursWorkedPerWeek(e,c) = 40
- IsEmployeeUnder5USC6301(e) = false
- LessThan50EmployeesWithin75MilesOfWorksite(e,c) = false
- IsAirlineFlightCrew(e,c) = true
- ReasonForRequestingLeaveFrom(e,c) = "To adopt a child"
- DateFamilyLeaveBegins(e,c) = 2014-01-01
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= "Stub"

Test: Test8_2612aC1_1
- Person e
- Corp c
- NumberOfEmployees(c) = 2000
- EmploymentRelationship(e,c) = "Employee"
- HoursWorkedPerWeek(e,c) = 40
- IsEmployeeUnder5USC6301(e) = false
- LessThan50EmployeesWithin75MilesOfWorksite(e,c) = false
- IsAirlineFlightCrew(e,c) = false
- ReasonForRequestingLeaveFrom(e,c) = "To care for family member with a health condition"
- DateFamilyLeaveBegins(e,c) = 2014-01-01
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= "Unstated"

Test: Test8_2612aC1_2
- Person e
- Person f
- Corp c
- NumberOfEmployees(c) = 2000
- EmploymentRelationship(e,c) = "Employee"
- HoursWorkedPerWeek(e,c) = 40
- IsEmployeeUnder5USC6301(e) = false
- LessThan50EmployeesWithin75MilesOfWorksite(e,c) = false
- IsAirlineFlightCrew(e,c) = false
- ReasonForRequestingLeaveFrom(e,c) = "To care for family member with a health condition"
- DateFamilyLeaveBegins(e,c) = 2014-01-01
- NeedsLeaveToProvideCareFor(e) = [[f]]
- AreMarried(e,f) = true
- HasSeriousHealthCondition(f) = true
- Sec2612.IsEntitledToRegLeaveFrom(e,c).AsOf(2011-04-15).Out =?= true



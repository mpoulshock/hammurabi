# Namespace:    IRS.Pub501
# Summary:      IRS Publication 501 - Who must file a tax return
# Updated:      2012-10-02
# Author:       Michael Poulshock



# RULES FOR TAX YEAR 2011

# TABLE 1 - MOST TAXPAYERS

# Table 1 (page 3)
Tbool MustFilePerTable1(Thing p) =
	Table1Applies(status, TaxAge(p), GrossIncome(p), SpousesUnder65(p))
	
	Tstr status = USC.Tit26.Sec2.FilingStatus(p) 

Tbool Table1Applies(Tstr status, Tnum age, Tnum grossIncome, Tnum spousesUnder65) =
	match status, age, grossIncome, spousesUnder65
	Single, 					< 65,	>= 9500,		-> true
	Single, 					>= 65,	>= 10950,		-> true
	Head of household, 			< 65,	>= 12200,		-> true
	Head of household, 			>= 65,	>= 13650,		-> true
	Married filing jointly,,			>= 19000,   2	-> true
	Married filing jointly,,			>= 20150,   1	-> true
	Married filing jointly,,			>= 21300,   0	-> true
	Married filing separately,,			>= 3700,		-> true
	Qualifying widow(er), 		< 65,	>= 15300,		-> true
	Qualifying widow(er), 		>= 65,	>= 16450,		-> true
	else false

# Number of people (person/spouse) under age 65	
Tnum SpousesUnder65(Thing p) =
	theSpouses.Filter(TaxAge(_) < 65).Count
	
	Tset theSpouses =
		Fam.SpousesOf(p) + p

# TABLE 2 - DEPENDENTS
		
# Table 2 (page 4) - filing requirements for dependents
Tbool MustFilePerTable2(Thing p) =
	MustFilePerTable2Single(p) |
	MustFilePerTable2Married(p)
	
# Table 2 - single dependents
Tbool MustFilePerTable2Single(Thing p) =
	Fam.MaritalStatus(p) == "Single" &
	...
		...
			! Over65OrBlind(p) &
			...
				UnearnedIncome(p) > 950 |
				EarnedIncome(p) > 5800 |
				GrossIncome(p) > Max(950, Min(EarnedIncome(p), 5500) + 300)
		|
		...
			Over65OrBlind(p) & ! Over65AndBlind(p) & 	# either 65+ or blind, but not both
			...
				UnearnedIncome(p) > 2400 |
				EarnedIncome(p) > 7250 |
				GrossIncome(p) > Max(2400, Min(EarnedIncome(p), 5500) + 1750)
		|
		...
			Over65AndBlind(p) &
			...
				UnearnedIncome(p) > 3850 |
				EarnedIncome(p) > 8700 |
				GrossIncome(p) > Max(3850, Min(EarnedIncome(p), 5500) + 3200)				
		
	Tbool claimable =
		USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(p)

# Table 2 - married dependents
Tbool MustFilePerTable2Married(Thing p) =
	Fam.MaritalStatus(p) == "Married" &
	...
		...
			! Over65OrBlind(p) &
			...
				MeetsSpouseItemizingTest(p) |
				UnearnedIncome(p) > 950 |
				EarnedIncome(p) > 5800 |
				GrossIncome(p) > Max(950, Min(EarnedIncome(p), 5500) + 300)
		|
		...
			Over65OrBlind(p) & ! Over65AndBlind(p) & 	# either 65+ or blind, but not both
			...
				MeetsSpouseItemizingTest(p) |
				UnearnedIncome(p) > 2100 |
				EarnedIncome(p) > 6950 |
				GrossIncome(p) > Max(2100, Min(EarnedIncome(p), 5500) + 1450)
		|
		...
			Over65AndBlind(p) &
			...
				MeetsSpouseItemizingTest(p) |
				UnearnedIncome(p) > 3250 |
				EarnedIncome(p) > 8100 |
				GrossIncome(p) > Max(3250, Min(EarnedIncome(p), 5500) + 2600)	
				
Tbool Over65OrBlind(Thing p) =
	Age(p) >= 65 |
	IsBlind(p)

Tbool Over65AndBlind(Thing p) =
	Age(p) >= 65 &
	IsBlind(p)

Tbool MeetsSpouseItemizingTest(Thing p) =
	GrossIncome(p) > 5 & 
	USC.Tit26.Sec2.FilingStatus(p) == "Married filing separately" &
	Fam.SpousesOf(p).Exists(ItemizesDeductions(_))
	
	
# Age on the last day of the calendar year
Tnum TaxAge(Thing p) =
	Age(p)	# Not correct
	

# TODO: Move these elsewhere...

# >>What is {1}'s gross income?
TnumIn? GrossIncome(Thing p) =
	EarnedIncome(p) +
	UnearnedIncome(p)

# >>What is {1}'s earned income?
TnumIn EarnedIncome(Thing p)	
# = salaries, wages, tips, professional fees, taxable scholarship/fellowship grants (see Table 2)

# >>What is {1}'s unearned income?
TnumIn UnearnedIncome(Thing p)
# = taxable interest, ordinary dividends, capital gain distributions, unemployment compensation, taxable
# social security benefits, pensions, annuities, and distributions of unearned income from a trust (see Table 2)

# >>Does/did {1} itemize deductions?
TboolIn ItemizesDeductions(Thing p)


# UNIT TESTS

Test: 738117373
- Thing liam
- CanBeClaimedAsDependentBySomeone(liam) = true
- MaritalStatus(liam) = "Single"
- DoB(liam) = 1975-01-01
- IsBlind(liam) = false
- UnearnedIncome(liam) = 2450
- IRS.Pub501.MustFilePerTable2(liam).TestOutput =?= "Time.DawnOf True "

Test: 417756183
- Thing ling
- FedTaxFilingStatus(ling) = "Married filing jointly"
- DoB(ling) = 1975-01-01
- GrossIncome(ling) = 19001
- Things john
- SpousesOf(ling) = [[john]]
- DoB(john) = 1981-01-01
- IRS.Pub501.MustFilePerTable1(ling).TestOutput =?= "Time.DawnOf True 1/1/2040 12:00:00 AM False 1/1/2091 12:00:00 AM True "

Test: 363173663
- Thing ling
- FedTaxFilingStatus(ling) = "Married filing jointly"
- DoB(ling) = 1910-01-01
- GrossIncome(ling) = 21000
- Things bing
- SpousesOf(ling) = [[bing]]
- DoB(bing) = 1910-01-01
- IRS.Pub501.MustFilePerTable1(ling).TestOutput =?= "Time.DawnOf True 1/1/1975 12:00:00 AM False 1/1/2020 12:00:00 AM True "

Test: 189988938
- Thing jam
- FedTaxFilingStatus(jam) = "Qualifying widow(er)"
- DoB(jam) = 1980-02-02
- GrossIncome(jam) = 16451
- IRS.Pub501.MustFilePerTable1(jam).TestOutput =?= "Time.DawnOf True "

Test: 844051182
- Thing lonnie
- DoB(lonnie) = 1980-01-01
- FedTaxFilingStatus(lonnie) = "Married filing separately"
- GrossIncome(lonnie) = 9
- IRS.Pub501.MustFilePerTable1(lonnie).TestOutput =?= "Time.DawnOf False "






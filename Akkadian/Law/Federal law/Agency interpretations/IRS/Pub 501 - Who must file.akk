# Namespace:    IRS.Pub501
# Summary:      IRS Publication 501 - Who must file a tax return
# Updated:      2012-10-04
# Author:       Michael Poulshock

##
TODO:
- Marital status on last day of tax year
- Age on last day of tax year
- Last day of tax year, temporal
- MFJ gross income problem 
- Gross income, defined
##

# RULES FOR TAX YEAR 2011

Tbool MustFileTaxReturn(Thing p) =
	set:
	if IRS.Pub519.IsNonresidentAlien(p) -> Stub()
	if HasIncomeFromUSPossession(p) -> Stub()
	if IsDeceased(p) -> DeceasedPersonMustFile(p)
	else CoreMustFileTest(p)

Tbool CoreMustFileTest(Thing p) =
	...
		USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(p) &
		MustFilePerTable2(p)
	|
	...
		! USC.Tit26.Sec152.CanBeClaimedAsDependentBySomeone(p) &
		MustFilePerTable1(p)
	|
	MustFilePerTable3(p)

	
# TABLE 1 - MOST TAXPAYERS

# Table 1 (page 3)
Tbool MustFilePerTable1(Thing p) =
	Table1Applies(status, TaxAge(p), GrossIncome(p), SpousesUnder65(p), LivedWithSpouseForTaxPurposes(p))
		
	Tstr status = USC.Tit26.Sec2.FilingStatus(p) 

Tbool Table1Applies(Tstr status, Tnum age, Tnum grossIncome, Tnum spousesUnder65, Tbool livedWSpouse) =
	match status, age, grossIncome, spousesUnder65, livedWSpouse
	Single, 					< 65,	>= 9500,,				-> true
	Single, 					>= 65,	>= 10950,,				-> true
	Head of household, 			< 65,	>= 12200,,				-> true
	Head of household, 			>= 65,	>= 13650,,				-> true
	Married filing jointly,,,,							false 	-> true 
	Married filing jointly,,			>= 19000,   2,			-> true
	Married filing jointly,,			>= 20150,   1,			-> true
	Married filing jointly,,			>= 21300,   0,			-> true
	Married filing separately,,			>= 3700,,				-> true
	Qualifying widow(er), 		< 65,	>= 15300,,				-> true
	Qualifying widow(er), 		>= 65,	>= 16450,,				-> true
	else false

# Number of people (person/spouse) under age 65	
Tnum SpousesUnder65(Thing p) =
	theSpouses.Filter(TaxAge(_) < 65).Count
	
	Tset theSpouses =
		Fam.SpousesOf(p) + p

# TABLE 2 - DEPENDENTS
		
# Table 2 (page 4) - filing requirements for dependents
Tbool MustFilePerTable2(Thing p) =
	MustFilePerTable2Single(p) |
	MustFilePerTable2Married(p)
	
# Table 2 - single dependents
Tbool MustFilePerTable2Single(Thing p) =
	Fam.MaritalStatus(p) == "Single" &
	...
		...
			! Over65OrBlind(p) &
			...
				UnearnedIncome(p) > 950 |
				EarnedIncome(p) > 5800 |
				GrossIncome(p) > Max(950, Min(EarnedIncome(p), 5500) + 300)
		|
		...
			Over65OrBlind(p) & ! Over65AndBlind(p) & 	# either 65+ or blind, but not both
			...
				UnearnedIncome(p) > 2400 |
				EarnedIncome(p) > 7250 |
				GrossIncome(p) > Max(2400, Min(EarnedIncome(p), 5500) + 1750)
		|
		...
			Over65AndBlind(p) &
			...
				UnearnedIncome(p) > 3850 |
				EarnedIncome(p) > 8700 |
				GrossIncome(p) > Max(3850, Min(EarnedIncome(p), 5500) + 3200)				

# Table 2 - married dependents
Tbool MustFilePerTable2Married(Thing p) =
	Fam.MaritalStatus(p) == "Married" &
	...
		...
			! Over65OrBlind(p) &
			...
				MeetsSpouseItemizingTest(p) |
				UnearnedIncome(p) > 950 |
				EarnedIncome(p) > 5800 |
				GrossIncome(p) > Max(950, Min(EarnedIncome(p), 5500) + 300)
		|
		...
			Over65OrBlind(p) & ! Over65AndBlind(p) & 	# either 65+ or blind, but not both
			...
				MeetsSpouseItemizingTest(p) |
				UnearnedIncome(p) > 2100 |
				EarnedIncome(p) > 6950 |
				GrossIncome(p) > Max(2100, Min(EarnedIncome(p), 5500) + 1450)
		|
		...
			Over65AndBlind(p) &
			...
				MeetsSpouseItemizingTest(p) |
				UnearnedIncome(p) > 3250 |
				EarnedIncome(p) > 8100 |
				GrossIncome(p) > Max(3250, Min(EarnedIncome(p), 5500) + 2600)	
				
Tbool Over65OrBlind(Thing p) =
	Age(p) >= 65 |
	IsBlind(p)

Tbool Over65AndBlind(Thing p) =
	Age(p) >= 65 &
	IsBlind(p)

Tbool MeetsSpouseItemizingTest(Thing p) =
	GrossIncome(p) > 5 & 
	USC.Tit26.Sec2.FilingStatus(p) == "Married filing separately" &
	Fam.SpousesOf(p).Exists(ItemizesDeductions(_))


# TABLE 3

# Table 3 (page 5) - other situations when you must file
Tbool MustFilePerTable3(Thing p) =
	# >>Does/did {1} owe the alternative minimum tax (AMT)?
	TboolIn OwesAlternativeMinimumTax(p) |
	# >>Does/did {1} owe an additional tax on a qualified plan?
	TboolIn OwesAdditionalTaxOnQualifiedPlan(p) |
	# >>Does/did {1} owe Social Security or Medicare taxes on unreported tips or wages that were not withheld?
	TboolIn OwesSocSecOrMedicareTaxes(p) |
	# >>Does/did {1} owe write-in taxes?
	TboolIn OwesWriteInTaxes(p) |
	# >>Does/did {1} owe household employment taxes?
	TboolIn OwesHouseholdEmploymentTaxes(p) |
	# >>Does/did {1} owe recapture taxes?
	TboolIn OwesRecaptureTaxes(p) |
	# >>Does/did {1} (or their spouse if filing jointly) receive Archer MSA, Medicare Advantage MSA, or health savings account distributions?
	TboolIn HadHSADistribution(p) |
	NetEarningsFromSelfEmployment(p) >= 400 |
	# >>Does/did {1} have over 108.28 in wages from a qualifying church-related organization?
	TboolIn HadThresholdChurchWages(p) |
	Stub()

# >>What are/were {1}'s net earnings from self-employment?
TnumIn NetEarningsFromSelfEmployment(Thing p)


# TODO: Move these elsewhere...?

# Deceased person's situation is analyzed as of their date of death
Tbool DeceasedPersonMustFile(Thing p) =
	CoreMustFileTest(p).AsOf(DateOfDeath(p))
	
# Age on the last day of the calendar year
Tnum TaxAge(Thing p) =
	Age(p)	# Not correct

# Marital status for tax purposes (determined on last day of tax year)
Tstr MaritalStatusForTaxPurposes(Thing p) =
	Stub()
	
# Lived with spouse for tax purposes
Tbool LivedWithSpouseForTaxPurposes(Thing p) =
	Fam.SpousesOf(p).Exists(LivedTogetherForTaxPurposes(p,_))
	
Tbool LivedTogetherForTaxPurposes(Thing spouse, Thing deceasedSpouse) =
	Econ.LivesWith(spouse,deceasedSpouse).AsOf(criticalDate)
	
	Tdate criticalDate =
		set:
		if IsDeceased(deceasedSpouse) -> DateOfDeath(deceasedSpouse)
		else 2011-12-31  # endOfTaxYear			# defined how?


# TODO: Gross income earned by an individual v. that attributable to a married person
# >>What is {1}'s gross income?
TnumIn? GrossIncome(Thing p) =
	EarnedIncome(p) +
	UnearnedIncome(p)

# >>What is {1}'s earned income?
TnumIn EarnedIncome(Thing p)	
# = salaries, wages, tips, professional fees, taxable scholarship/fellowship grants (see Table 2)

# >>What is {1}'s unearned income?
TnumIn UnearnedIncome(Thing p)
# = taxable interest, ordinary dividends, capital gain distributions, unemployment compensation, taxable
# social security benefits, pensions, annuities, and distributions of unearned income from a trust (see Table 2)

# >>Does/did {1} itemize deductions?
TboolIn ItemizesDeductions(Thing p)

# >>Does/did {1} have income from a U.S. possession (Guam, N. Mariana Islands, American Samoa, or the U.S. Virgin Islands)?
TboolIn HasIncomeFromUSPossession(Thing p)


# UNIT TESTS

Test: 738117373
- Thing liam
- CanBeClaimedAsDependentBySomeone(liam) = true
- MaritalStatus(liam) = "Single"
- DoB(liam) = 1975-01-01
- IsBlind(liam) = false
- UnearnedIncome(liam) = 2450
- IRS.Pub501.MustFilePerTable2(liam).TestOutput =?= "Time.DawnOf True "

Test: 417756183
- Thing ling
- FedTaxFilingStatus(ling) = "Married filing jointly"
- DoB(ling) = 1975-01-01
- GrossIncome(ling) = 19001
- Things john
- SpousesOf(ling) = [[john]]
- DoB(john) = 1981-01-01
- IRS.Pub501.MustFilePerTable1(ling).TestOutput =?= "Time.DawnOf True 1/1/2040 12:00:00 AM False 1/1/2091 12:00:00 AM True "

Test: 363173663
- Thing ling
- FedTaxFilingStatus(ling) = "Married filing jointly"
- DoB(ling) = 1910-01-01
- GrossIncome(ling) = 21000
- Things bing
- SpousesOf(ling) = [[bing]]
- DoB(bing) = 1910-01-01
- IRS.Pub501.MustFilePerTable1(ling).TestOutput =?= "Time.DawnOf True 1/1/1975 12:00:00 AM False 1/1/2020 12:00:00 AM True "

Test: 189988938
- Thing jam
- FedTaxFilingStatus(jam) = "Qualifying widow(er)"
- DoB(jam) = 1980-02-02
- GrossIncome(jam) = 16451
- IRS.Pub501.MustFilePerTable1(jam).TestOutput =?= "Time.DawnOf True "

Test: 844051182
- Thing lonnie
- DoB(lonnie) = 1980-01-01
- FedTaxFilingStatus(lonnie) = "Married filing separately"
- GrossIncome(lonnie) = 9
- IRS.Pub501.MustFilePerTable1(lonnie).TestOutput =?= "Time.DawnOf False "

